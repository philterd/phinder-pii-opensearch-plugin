apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.yaml-rest-test'
apply plugin: 'opensearch.pluginzip'

group = "ai.philterd"
version = "3.2.0-SNAPSHOT"

opensearchplugin {
    name 'phinder'
    description 'Phinder'
    classname "ai.philterd.phinder.PhinderPlugin"
    licenseFile rootProject.file('LICENSE.txt')
    noticeFile rootProject.file('NOTICE.txt')
}

thirdPartyAudit.enabled = false
forbiddenApisTest.enabled = false
validateNebulaPom.enabled = false
loggerUsageCheck.enabled = false
dependencyLicenses.enabled = false

java {
    targetCompatibility = JavaVersion.VERSION_21
    sourceCompatibility = JavaVersion.VERSION_21
}

ext {
    phileasVersion = '2.12.3'
}

buildscript {

    ext {
        opensearch_version = System.getProperty("opensearch.version", "3.2.0")
    }

    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }

}

dependencies {
    implementation("ai.philterd:phileas-core:$phileasVersion")
    implementation("ai.philterd:phileas-model:$phileasVersion")
    implementation("ai.philterd:phileas-services-policies:$phileasVersion")
    implementation("ai.philterd:phileas-services-anonymization:$phileasVersion")
    implementation("ai.philterd:phileas-services-alerts:$phileasVersion")
    implementation("ai.philterd:phileas-processors-unstructured:$phileasVersion")
    implementation("ai.philterd:phileas-services-disambiguation:$phileasVersion")
    implementation("ai.philterd:phileas-services-split:$phileasVersion")

    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.json:json:20250517'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.20.1'
    implementation 'org.apache.commons:commons-collections4:4.5.0'

    testImplementation("org.opensearch:opensearch-agent-bootstrap:${opensearch_version}")
    yamlRestTestImplementation("org.opensearch:opensearch-agent-bootstrap:${opensearch_version}")

}

repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://artifacts.philterd.ai/releases" }
    maven { url "https://artifacts.philterd.ai/snapshots" }
}

publishing {
    publications {
        pluginZip(MavenPublication) { publication ->
            artifact bundlePlugin
            pom {
                name = "phinder-pii-opensearch-plugin"
                description = "An OpenSearch plugin to redact PII from search results"
                groupId = "ai.philterd"

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'philterd'
                        name = 'jzonthemtn'
                        email = 'jeff.zemerick@philterd.ai'
                        organization = 'Philterd, LLC'
                        organizationUrl = 'https://www.philterd.ai'
                        url = 'https://www.philterd.ai'
                    }
                }
            }
        }
        all {
            pom {
                name = "phinder-pii-opensearch-plugin"
                description = "An OpenSearch plugin to redact PII from search results"
            }
        }
    }
}

test {
    systemProperty 'tests.security.manager', 'false'
}

yamlRestTest {
    systemProperty 'tests.security.manager', 'false'
}